apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
apply from: "../bitmovinpropertiesloader.gradle"

def packageName = 'com.bitmovin.player.integration'
def libraryVersion = '1.18.4'
def libraryCode = 61

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 31
        versionCode libraryCode
        versionName libraryVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


    kotlinOptions { jvmTarget = '11' }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
    namespace 'com.bitmovin.player.integration.yospace'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation yospaceDependencies.adManagementSDK
    implementation yospaceDependencies.adManagementUtil
    annotationProcessor 'androidx.lifecycle:lifecycle-common-java8:2.3.1'

    testImplementation testDependencies.junitAPI
    testRuntimeOnly testDependencies.junitEngine
    testImplementation testDependencies.junitParams

    implementation supportDependencies.appCompat
    implementation bitmovinPlayerDependencies.bitmovinPlayer
    implementation truexDependencies.truexRenderer
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId packageName
            version = libraryVersion
            artifactId project.getName()

            // Tell maven to prepare the generated "*.aar" file for publishing
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Defining configuration names from which dependencies will be taken (debugCompile or releaseCompile and compile)
                def configurationNames = ['compile']

                configurationNames.each { configurationName ->
                    configurations[configurationName].allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = 'https://bitmovin.jfrog.io/bitmovin'
    publish {
        repository {
            // The Artifactory repository key to publish to
            repoKey = libraryVersion.endsWith('SNAPSHOT') ? 'libs-snapshot-local' : 'libs-release-local'
        }
        defaults {
            // Tell the Artifactory Plugin which artifacts should be published to Artifactory.
            publications('aar')
            publishArtifacts = true
            username = bitmovinProperties.getProperty('artifactory_user') // The publisher user name
            password = bitmovinProperties.getProperty('artifactory_password') // The publisher password

            // Properties to be attached to the published artifacts.
            properties = ['qa.level': 'basic', 'dev.team': 'core']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}
